apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply from: '../config/quality.gradle'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.3"
    defaultConfig {
        applicationId "com.staceybellerose.randomwordgenerator"
        resValue "string", "application_id", "${applicationId}"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode gitVersion.version
        versionName "2.0.0." + gitVersion.name
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        release {
            if (project.hasProperty('RELEASE_STORE_FILE')) {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        def BOOLEAN = "boolean"
        def TRUE = "true"
        def FALSE = "false"
        def CLEAN_WORDS_ONLY = "CLEAN_WORDS_ONLY"
        def CAN_DOWNLOAD = "CAN_DOWNLOAD"
        basic {
            buildConfigField BOOLEAN, CLEAN_WORDS_ONLY, FALSE
            buildConfigField BOOLEAN, CAN_DOWNLOAD, FALSE
        }
        clean {
            buildConfigField BOOLEAN, CLEAN_WORDS_ONLY, TRUE
            buildConfigField BOOLEAN, CAN_DOWNLOAD, FALSE
            applicationIdSuffix ".clean"
            versionNameSuffix "-clean"
        }
        full {
            buildConfigField BOOLEAN, CLEAN_WORDS_ONLY, FALSE
            buildConfigField BOOLEAN, CAN_DOWNLOAD, TRUE
            applicationIdSuffix ".full"
            versionNameSuffix "-full"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.outputFile
            def appName = rootProject.ext.appName
            def fileName = appName + "-" + variant.buildType.name + "-" + variant.versionName.replaceAll("[.]", "-") + ".apk"
            output.outputFile = new File(file.parent, fileName)
        }
        def applicationId = [variant.mergedFlavor.applicationId, variant.mergedFlavor.applicationIdSuffix, variant.buildType.applicationIdSuffix].findAll().join()
        variant.resValue "string", "application_id", "${applicationId}"
        task "open${variant.name.capitalize()}" {
            group = 'verification'
            description 'Open the launcher activity of the app on a connected device via adb'
            dependsOn "install${variant.name.capitalize()}"

            doLast {
                exec {
                    commandLine "cmd /c adb shell monkey -p ${variant.applicationId} -c android.intent.category.LAUNCHER 1".split(" ")
                }
            }
        }
    }
    lintOptions {
        disable 'InvalidPackage', 'OldTargetApi', 'GoogleAppIndexingWarning', 'InconsistentArrays', 'VectorRaster'
    }
}

dependencies {
    ext {
        support_library_version = '26.1.0'
    }
    compile "com.android.support:appcompat-v7:${support_library_version}"
    compile "com.android.support:design:${support_library_version}"
    compile "com.android.support:cardview-v7:${support_library_version}"
    compile "com.android.support:recyclerview-v7:${support_library_version}"
    compile "com.android.support:support-v4:${support_library_version}"
    compile 'com.github.gabrielemariotti.changeloglib:changelog:2.1.0'
    compile 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.6.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.1'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.1'
    compile 'io.reactivex.rxjava2:rxandroid:2.1.0'
    compile 'io.reactivex.rxjava2:rxjava:2.2.2'
    compile 'com.github.davidmoten:rxjava2-extras:0.1.25'
    compile 'com.jonathanfinerty.once:once:1.2.2'
    compile 'uk.co.samuelwall:material-tap-target-prompt:2.4.1'
    compile('com.mikepenz:aboutlibraries:5.9.7@aar') {
        transitive = true
    }
}
